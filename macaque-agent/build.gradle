plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'six.eared.macaque'

repositories {
    mavenCentral()
}

dependencies {
    implementation files("${System.getProperty('java.home')}/../lib/tools.jar")
    implementation project(':macaque-mbean')
    implementation project(':macaque-common')
    implementation project(':library:macaque-library')
    implementation 'org.ow2.asm:asm:9.3'
    implementation 'org.ow2.asm:asm-tree:9.3'
    implementation 'org.projectlombok:lombok:1.18.24'
    implementation 'io.github.hhy50:reflect-linker:1.0.6'
    implementation('com.taobao.arthas:arthas-vmtool:4.0.3') {
        exclude group: 'com.taobao.arthas', module: 'arthas-common'
    }
    testImplementation 'junit:junit:4.13.1'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

test {
    useJUnit()
}

shadowJar {
    dependencies {
        exclude 'tools.jar'
    }
    def libs = allLibrary()
    libs.collect { File libDir ->
        def modName = ":library:${libDir.name}"
        dependsOn "${modName}:compileJava"
        from ("${project(modName).buildDir}/classes/java/main/") {
            into '/'
        }
        from ("${project(modName).projectDir}/src/main/resources/") {
            into '/'
        }
    }

    archiveBaseName = 'agent'
    archiveVersion = ''
    archiveClassifier = null
}

task release(type: Jar) {
    from(shadowJar) {
        into('lib/')
    }

    from('build/classes/java/main/six/eared/macaque/agent/loader/') {
        into('six/eared/macaque/agent/loader')
    }

    archiveBaseName = "$project.name"
    archiveVersion = ''
    archiveClassifier = null
    manifest {
        attributes(
                'Agent-Class': 'six.eared.macaque.agent.loader.AgentMain',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Manifest-Version': "${projectVersion}"
        )
    }

    doLast {
        delete file("${project.buildDir}/libs/agent.jar")
    }
}

task lightwidhtJar(type: Jar, dependsOn: compileJava) {
    from('build/classes/java/main/six/eared/macaque/agent/LightweightAgent.class') {
        into('six/eared/macaque/agent/')
    }

    archiveBaseName = "$project.name"
    archiveVersion = "lightweight"
    archiveClassifier = null
    manifest {
        attributes(
                'Agent-Class': 'six.eared.macaque.agent.LightweightAgent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Manifest-Version': "${projectVersion}"
        )
    }
}

File[] allLibrary() {
    def modulesDir = new File(project.parent.projectDir, 'library')
    return modulesDir.listFiles({
        it.isDirectory() && it.name != 'macaque-library'
    } as FileFilter)
}