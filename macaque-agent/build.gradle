plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'six.eared.macaque'

repositories {
    mavenCentral()
}

dependencies {
    implementation files("${System.getenv('JAVA_HOME')}/lib/tools.jar")
    implementation project(':macaque-mbean')
    implementation project(':macaque-common')
    implementation project(':macaque-asm')
    implementation project(':library:macaque-library')
    implementation project(':library:macaque-mybatis')
    implementation project(':library:macaque-spring')
    testImplementation 'junit:junit:4.13.1'
    testImplementation files("${System.getenv('JAVA_HOME')}/lib/tools.jar")
}


shadowJar {
    dependencies {
        exclude 'tools.jar'
    }

    archiveBaseName = "$project.name"
    archiveVersion = ""
    archiveClassifier = null
    manifest {
        attributes(
                'Agent-Class': 'six.eared.macaque.agent.AgentMain',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Manifest-Version': "${projectVersion}"
        )
    }
}

task lightwidhtJar(type: Jar, dependsOn: compileJava) {
    from('build/classes/java/main/six/eared/macaque/agent/LightweightAgent.class') {
        into('six/eared/macaque/agent/')
    }

    archiveBaseName = "$project.name"
    archiveVersion = "lightweight"
    archiveClassifier = null
    manifest {
        attributes(
                'Agent-Class': 'six.eared.macaque.agent.LightweightAgent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Manifest-Version': "${projectVersion}"
        )
    }
}