plugins {
    id 'java'
    id 'application'
}

group = 'six.eared.macaque'
version = projectVersion

def projectName = 'macaque-server'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation project(':macaque-http')
    implementation project(':macaque-core')

    testImplementation 'junit:junit:4.13.1'
}

jar {
    archiveBaseName = "${projectName}"
    archiveVersion = ''
    manifest {
        attributes(
                'Main-Class': 'six.eared.macaque.server.MacaqueBootStrap',
                'Manifest-Version': "${projectVersion}",
                'Class-Path': configurations.runtimeClasspath.files.collect {
                    it.getName().contains("tools.jar") ? "" : "../libs/" + it.getName()
                }.join(' ')
        )
    }
}

task release(type: Tar, group: build) {
    from(jar) {
        into('bin/')
    }
    from(project(":macaque-agent").shadowJar) {
        into('bin/')
    }
    from("${rootDir}/bin") {
        into('bin/')
    }

    from(project(":macaque-common").jar) {
        into('libs/')
    }
    from(project(":macaque-core").jar) {
        into('libs/')
    }
    from(project(":macaque-mbean").jar) {
        into('libs/')
    }
    from(project(":macaque-http").jar) {
        into('libs/')
    }

    from(configurations.runtimeClasspath.files) {
        exclude "macaque-*-${projectVersion}.jar"
        exclude 'tools.jar'
        into('libs/')
    }

    compression = Compression.GZIP
    archiveBaseName = "${projectName}"
    archiveVersion = "${projectVersion}"
    archiveExtension = 'tar.gz'
}

